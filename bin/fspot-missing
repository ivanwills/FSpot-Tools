#!/usr/bin/perl

# Created on: 2009-06-16 21:02:08
# Create by:  Ivan Wills
# $Id$
# $Revision$, $HeadURL$, $Date$
# $Revision$, $Source$, $Date$

use strict;
use warnings;
use version;
use Getopt::Long;
use Pod::Usage;
use List::MoreUtils qw/any/;
use Data::Dumper qw/Dumper/;
use English qw/ -no_match_vars /;
use FindBin qw/$Bin/;
use FSpot;
use Image::ExifTool;
use Path::Class;
use DateTime;
use File::stat;

our $VERSION = version->new('0.0.1');
my ($name)   = $PROGRAM_NAME =~ m{^.*/(.*?)$}mxs;

my %option = (
	verbose => 0,
	man     => 0,
	help    => 0,
	VERSION => 0,
);

main();
exit 0;

sub main {

	Getopt::Long::Configure('bundling');
	GetOptions(
		\%option,
		'db|d',
		'files|f',
		'ignore|i=s',
		'limit|l=i',
		'test|t',
		'verbose|v+',
		'man',
		'help',
		'VERSION!',
	) or pod2usage(2);

	if ( $option{'VERSION'} ) {
		print "$name Version = $VERSION\n";
		exit 1;
	}
	elsif ( $option{'man'} ) {
		pod2usage( -verbose => 2 );
	}
	elsif ( $option{'help'} ) {
		pod2usage( -verbose => 1 );
	}

	# check if there are any directories to ignore and convert to array ref
	if ( $option{ignore} ) {
		$option{ignore} = [ split /[:;]/, $option{ignore} ];
	}

	my $fspot  = FSpot->new;
	my $photos = $fspot->schema->resultset('PhotoVersions')->search;
	my $count  = 0;

	PHOTO:
	while ( my $photov = $photos->next ) {

		my $file = $photov->uri;
		$file =~ s{^file://}{}xms;
		$file =~ s{%(\d\d)}{chr hex $1}gexms;
		$file = file($file);

		next PHOTO if -f $file;

		my $name     = $file->basename;
		my $quoted   = $name;
		$name   =~ s{ ( [[(*+)\]] ) }{\\$1}gxms;
		$quoted =~ s/"/\\"/gxms;
		my @versions = grep {/$name$/} split /\n/, `locate "$quoted"`;

		# check if any found image version should be ignored
		if ( $option{ignore} && @versions ) {
			my @new_versions;
			for my $version ( @versions ) {
				push @new_versions, $version if !any { $version =~ /^$_/ } @{ $option{ignore} };
			}

			# remove ignored file versions
			@versions = @new_versions;
		}

		if ( !@versions || @versions > 1 ) {
			# There is nothing that we can do/safly do to fix this photo
			print
				!@versions       ? "completely missing\t$file `locate \"$quoted\"`\n"
				:                  "Several options found for\t$file (".$photov->id.")\n";
			print "Versions:\n" . (join "\n", @versions) . "\n" if $option{verbose} > 1 && @versions && @versions > 1;
		}
		elsif ( $option{db} ) {
			# Fix the database to point to the correct file
			fix_db( $fspot, $photov, $file, \@versions,  );
		}
		elsif ( $option{file} ) {
			# move the file to the location (not yet implemented)
			print "moveing $file\tto $versions[0]\n";
		}
		else {
			# tell the user what could be done to fix the issue
			print "Could attempt to fix $file\twith $versions[0] (using --db or --file)\n";
		}

		# check if we should stop before we have finished checking all files
		last PHOTO if $option{limit} && @versions && @versions == 1 && $count++ >= $option{limit};
	}

	return;
}

sub fix_db {
	my ($fspot, $photov, $file, $versions, ) = @_;

	print "updating $file\tto point to $versions->[0]\n";

	my $old = $photov->uri =~ /^file:/ ? $photov->uri : 'file://' . $photov->uri;
	my $new = 'file://' . $versions->[0];
	$new =~ s{ ( [^-\w\s/:\().] ) }{sprintf "%%%x", ord $1}gexms;
	die $new if $new =~ /\w2e(?:jpg|nef|tif)$/ixms;

	if ( !$option{test} ) {
		$fspot->backup_db;

		$new = 'file://' . $new if $new !~ /^file:/;
		$photov->uri( $new );
		$photov->update;

		if ( $photov->version_id == 1 ) {
			$photov->photo_id->uri($new);
			$photov->photo_id->update;
		}
	}

	return;
}

__DATA__

=head1 NAME

fspot-missing - Find any files that are in the fspot db but missing on the
file system

=head1 VERSION

This documentation refers to fspot-missing version 0.1.

=head1 SYNOPSIS

   fspot-missing [option]

 OPTIONS:
  -d --db      Fix the f-spot db's file locations (if only one matches)
  -f --files   Move files to match f-spot's location (if only one matches)
  -i --ignore=regex
               Ignore matching files found in directories that match this
               regex. (eg ignore back/copy directories so fix options can
               work more of the time)

  -v --verbose Show more detailed option
     --version Prints the version information
     --help    Prints this help information
     --man     Prints the full documentation for fspot-missing

=head1 DESCRIPTION

=head1 SUBROUTINES/METHODS

=head1 DIAGNOSTICS

=head1 CONFIGURATION AND ENVIRONMENT

=head1 DEPENDENCIES

=head1 INCOMPATIBILITIES

=head1 BUGS AND LIMITATIONS

There are no known bugs in this module.

Please report problems to Ivan Wills (ivan.wills@gmail.com).

Patches are welcome.

=head1 AUTHOR

Ivan Wills - (ivan.wills@gmail.com)

=head1 LICENSE AND COPYRIGHT

Copyright (c) 2009 Ivan Wills (14 Mullion Close, Hornsby Heights, NSW, Australia 2077).
All rights reserved.

This module is free software; you can redistribute it and/or modify it under
the same terms as Perl itself. See L<perlartistic>.  This program is
distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY;
without even the implied warranty of MERCHANTABILITY or FITNESS FOR A
PARTICULAR PURPOSE.

=cut
